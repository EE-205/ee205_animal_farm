###############################################################################
##  University of Hawaii, College of Engineering
##  Animal Farm - EE 205 - Spring 2024
##
### CMake include file dedicated to Doxygen
###
### @see https://cmake.org/cmake/help/latest/manual/cmake-commands.7.html
###
### @file   CMake_Doxygen.txt
### @author Mark Nelson <marknels@hawaii.edu>
###############################################################################

set(DOC_BUILD_DIR "${PROJECT_BINARY_DIR}/docs")

find_package(Doxygen
        REQUIRED dot
        OPTIONAL_COMPONENTS mscgen dia)

if (DOXYGEN_FOUND)
    message(STATUS "Generating documentation using Doxygen")

    # Project
    set(DOXYGEN_PROJECT_NUMBER ${CMAKE_PROJECT_VERSION})
    set(DOXYGEN_PROJECT_BRIEF ${PROJECT_DESCRIPTION})
    set(DOXYGEN_PROJECT_LOGO "images/animal_farm_logo.jpg")
    set(DOXYGEN_OUTPUT_DIRECTORY ${CMAKE_SOURCE_DIR}/docs)
    set(DOXYGEN_STRIP_FROM_PATH ${PROJECT_PARENT_DIR})
    set(DOXYGEN_JAVADOC_AUTOBRIEF YES)
    set(DOXYGEN_TAB_SIZE 3)
    set(DOXYGEN_ALIASES "pattern=@par Design Pattern: "       # Create a custom @pattern tag
            "NOLINT=@par Excluded Warnings: ^^"   # Create a custom @NOLINT tag
    )
    set(DOXYGEN_OPTIMIZE_OUTPUT_FOR_C YES)
    set(DOXYGEN_BUILTIN_STL_SUPPORT YES)

    # Build
    set(DOXYGEN_EXTRACT_PRIVATE YES)
    set(DOXYGEN_EXTRACT_PRIV_VIRTUAL YES)
    set(DOXYGEN_EXTRACT_PACKAGE YES)
    set(DOXYGEN_EXTRACT_STATIC YES)
    set(DOXYGEN_EXTRACT_LOCAL_METHODS YES)
    set(DOXYGEN_INTERNAL_DOCS YES)
    set(DOXYGEN_CASE_SENSE_NAMES YES)
    set(DOXYGEN_SORT_MEMBERS_CTORS_1ST YES)   # Put constructors first

    # Messages
    set(DOXYGEN_QUIET YES)
    #  set(DOXYGEN_WARN_AS_ERROR FAIL_ON_WARNINGS)   # @todo Consider turning this off for students

    # Input
    set(DOXYGEN_IMAGE_PATH images/)
    set(DOXYGEN_USE_MDFILE_AS_MAINPAGE "README.md")

    # Source Browser
    set(DOXYGEN_SOURCE_BROWSER YES)           # @todo Turn this off for students
    set(DOXYGEN_STRIP_CODE_COMMENTS NO)
    set(DOXYGEN_CLANG_ASSISTED_PARSING NO)
    set(DOXYGEN_CLANG_OPTIONS "-std=c++17 -I /usr/include/llvm -I /usr/lib/clang/14.0.6/include" )

    # Index

    # HTML
    set(DOXYGEN_GENERATE_HTML YES)
    set(DOXYGEN_HTML_EXTRA_STYLESHEET "css/extraStyleSheet.css")
    set(DOXYGEN_HTML_COLORSTYLE LIGHT)
    set(DOXYGEN_HTML_TIMESTAMP YES)
    set(DOXYGEN_GENERATE_TREEVIEW YES)

    # LaTeX

    # RTF

    # Man

    # XML

    # Docbook

    # Autogen

    # Pearlmod

    # Preprocessor
    set(DOXYGEN_MACRO_EXPANSION YES)
    set(DOXYGEN_EXPAND_ONLY_PREDEF YES)

    # External

    # Dot
    set(DOXYGEN_HAVE_DOT YES)
    set(DOXYGEN_DOT_NUM_THREADS 1)
    set(DOXYGEN_UML_LOOK YES)
    set(DOXYGEN_UML_LIMIT_NUM_FIELDS 100)
    set(DOXYGEN_DOT_UML_DETAILS YES)
    set(DOXYGEN_DOT_WRAP_THRESHOLD 80)
    set(DOXYGEN_TEMPLATE_RELATIONS YES)
    set(DOXYGEN_CALL_GRAPH YES)
    set(DOXYGEN_CALLER_GRAPH YES)
    set(DOXYGEN_DOT_IMAGE_FORMAT svg)
    set(DOXYGEN_INTERACTIVE_SVG YES)
    set(DOXYGEN_DOT_GRAPH_MAX_NODES 100)
    set(DOXYGEN_DOT_MULTI_TARGETS YES)

    #  set(DOXYGEN_DOT_TRANSPARENT YES)
    #  set(DOXYGEN_FULL_SIDEBAR NO)
    #  set(DOXYGEN_DISABLE_INDEX NO)
    #  set(DOXYGEN_PAPER_TYPE letter)
    #  set(DOXYGEN_VERBATIM_HEADERS YES)

    message(STATUS "DOXYGEN_OUTPUT_DIRECTORY = [${DOXYGEN_OUTPUT_DIRECTORY}]")

    doxygen_add_docs(Doxygen
            ${CMAKE_SOURCE_DIR}/src
            ${CMAKE_SOURCE_DIR}/README.md
            ${CMAKE_SOURCE_DIR}/CONTRIBUTE.md
            ${CMAKE_SOURCE_DIR}/DOXYGEN.md
            ${CMAKE_SOURCE_DIR}/RELEASE.md
            ${CMAKE_SOURCE_DIR}/tests/INSTALLING_BOOST_TESTS.md
            ${CMAKE_SOURCE_DIR}/STATISTICS.md
            ${CMAKE_SOURCE_DIR}/WEEKLY_PROGRESS.md
            ALL
            COMMENT "Generating doxygen documentation for ${PROJECT_NAME} to ${DOXYGEN_OUTPUT_DIRECTORY}"
    )

    add_custom_command(TARGET Doxygen
            PRE_BUILD
            COMMAND rsync --recursive --mkpath --checksum --delete ${CMAKE_SOURCE_DIR}/images ${DOXYGEN_OUTPUT_DIRECTORY}/html
            COMMENT "Updating images directory"
    )

    add_custom_command(TARGET Doxygen
            PRE_BUILD
            COMMAND ./bin/stats.py
            COMMENT "Updating statistics"
            WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}
    )

    add_custom_command(TARGET Doxygen
            POST_BUILD
            COMMAND rsync --recursive --checksum --delete --compress --stats --chmod=o+r,Do+x ${DOXYGEN_OUTPUT_DIRECTORY}/html/ marknels@uhunix.hawaii.edu:~/public_html/ee205/${CMAKE_PROJECT_NAME}
            COMMENT "Pushing files to UHUnix"
    )

else (DOXYGEN_FOUND)
    message("Doxygen needs to be installed to generate the doxygen documentation")
endif (DOXYGEN_FOUND)
